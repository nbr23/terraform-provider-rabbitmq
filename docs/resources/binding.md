---
layout: "rabbitmq"
page_title: "RabbitMQ: rabbitmq_binding"
sidebar_current: "docs-rabbitmq-resource-binding"
description: |-
  Creates and manages a binding on a RabbitMQ server.
---

# rabbitmq\_binding

The ``rabbitmq_binding`` resource creates and manages a binding relationship
between a queue an exchange.

## Example Usage

```hcl
resource "rabbitmq_vhost" "test" {
  name = "test"
}

resource "rabbitmq_permissions" "guest" {
  user  = "guest"
  vhost = "${rabbitmq_vhost.test.name}"

  permissions {
    configure = ".*"
    write     = ".*"
    read      = ".*"
  }
}

resource "rabbitmq_exchange" "test" {
  name  = "test"
  vhost = "${rabbitmq_permissions.guest.vhost}"

  settings {
    type        = "fanout"
    durable     = false
    auto_delete = true
  }
}

resource "rabbitmq_queue" "test" {
  name  = "test"
  vhost = "${rabbitmq_permissions.guest.vhost}"

  settings {
    durable     = true
    auto_delete = false
  }
}

resource "rabbitmq_binding" "test" {
  source           = "${rabbitmq_exchange.test.name}"
  vhost            = "${rabbitmq_vhost.test.name}"
  destination      = "${rabbitmq_queue.test.name}"
  destination_type = "queue"
  routing_key      = "#"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `destination` (String) The destination queue or exchange.
- `destination_type` (String) The type of the destination. Must be either `queue` or `exchange`.
- `source` (String) The source exchange.
- `vhost` (String) The vhost to create the resource in.

### Optional

- `arguments` (Map of String) Additional key/value arguments for the binding. Conflicts with `arguments_json`
- `arguments_json` (String) Additional key/value arguments for the binding in JSON format. Conflicts with `arguments`
- `routing_key` (String) A routing key for the binding.

### Read-Only

- `id` (String) The ID of this resource.
- `properties_key` (String) A unique key to refer to the binding.

## Import

Bindings can be imported using the `id` which is composed of
  `vhost/source/destination/destination_type/properties_key`. E.g.

```
$ terraform import rabbitmq_binding.test test/test/test/queue/%23
```
