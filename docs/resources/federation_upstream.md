---
layout: "rabbitmq"
page_title: "RabbitMQ: rabbitmq_federation_upstream"
sidebar_current: "docs-rabbitmq-resource-federation-upstream"
description: |-
  Creates and manages a federation upstream on a RabbitMQ server.
---

# rabbitmq\_federation\_upstream

The ``rabbitmq_federation_upstream`` resource creates and manages a federation upstream parameter.

## Example Usage

```hcl
resource "rabbitmq_vhost" "test" {
  name = "test"
}

resource "rabbitmq_permissions" "guest" {
  user = "guest"
  vhost = rabbitmq_vhost.test.name
  permissions {
    configure = ".*"
    write = ".*"
    read = ".*"
  }
}

// downstream exchange
resource "rabbitmq_exchange" "foo" {
  name  = "foo"
  vhost = rabbitmq_permissions.guest.vhost

  settings {
    type    = "topic"
    durable = "true"
  }
}

// upstream broker
resource "rabbitmq_federation_upstream" "foo" {
  name = "foo"
  vhost = rabbitmq_permissions.guest.vhost

  definition {
    uri = "amqp://guest:guest@upstream-server-name:5672/%2f"
    prefetch_count = 1000
    reconnect_delay = 5
    ack_mode = "on-confirm"
    trust_user_id = false
    max_hops = 1
  }
}

resource "rabbitmq_policy" "foo" {
  name  = "foo"
  vhost = rabbitmq_permissions.guest.vhost

  policy {
    pattern  = "(^${rabbitmq_exchange.foo.name}$)"
    priority = 1
    apply_to = "exchanges"

    definition = {
      federation-upstream = rabbitmq_federation_upstream.foo.name
    }
  }
}

```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `definition` (Block List, Min: 1, Max: 1) The configuration of the federation upstream. (see [below for nested schema](#nestedblock--definition))
- `name` (String) The name of the federation upstream.
- `vhost` (String) The vhost to create the resource in.

### Read-Only

- `component` (String) Set to `federation-upstream` by the underlying RabbitMQ provider. You do not set this attribute but will see it in state and plan output.
- `id` (String) The ID of this resource.

<a id="nestedblock--definition"></a>
### Nested Schema for `definition`

Required:

- `uri` (String, Sensitive) The AMQP URI(s) for the upstream. Note that the URI may contain sensitive information, such as a password.

Optional:

- `ack_mode` (String) Determines how the link should acknowledge messages. Valid values are `on-confirm`, `on-publish`, and `no-ack`.
- `exchange` (String) The name of the upstream exchange. This is only applicable to federated exchanges.
- `expires` (Number) The expiry time (in milliseconds) after which an upstream queue for a federated exchange may be deleted if a connection to the upstream is lost. This is only applicable to federated exchanges.
- `max_hops` (Number) Maximum number of federation links that messages can traverse before being dropped. This is only applicable to federated exchanges.
- `message_ttl` (Number) The expiry time (in milliseconds) for messages in the upstream queue for a federated exchange (see expires). This is only applicable to federated exchanges.
- `prefetch_count` (Number) Maximum number of unacknowledged messages that may be in flight over a federation link at one time.
- `queue` (String) The name of the upstream queue. This is only applicable to federated queues.
- `reconnect_delay` (Number) Time in seconds to wait after a network link goes down before attempting reconnection.
- `trust_user_id` (Boolean) Determines how federation should interact with the validated user-id feature.

## Import

A Federation upstream can be imported using the resource `id` which is composed of `name@vhost`, e.g.

```sh
terraform import rabbitmq_federation_upstream.foo foo@test
```
